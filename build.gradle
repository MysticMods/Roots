buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${minecraft_version}-${mod_version}"
group = "epicsquid.roots"
archivesBaseName = "Roots"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "Roots.java"
    replace "after:maindependencies", project.depends
    replaceIn "MysticalWorld.java"

    // Scala: not even once
    replace 'scala.actors.threadpool.Arrays', 'java.util.Arrays'
}

dependencies {
    if (!project.hasProperty("dev_env")) {
        compile files('libs/')
    } else {
        compile project(':MysticalLib')
        compile project(':MysticalWorld')
        compileOnly "vazkii.patchouli:Patchouli:${patchouli_version}"
        deobfCompile "com.azanor:Baubles:${version_baubles}"
    }

    // compile against the JEI API but do not include it at runtime
    deobfProvided "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    compileOnly("jeresources:JustEnoughResources-${minecraft_version}:${jer_version}") {
        exclude group: 'mezz.jei'
    }
    compile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${ct_version}") {
        exclude group: 'mezz.jei'
    }
    compileOnly("vazkii.botania:Botania:${botania_version}") {
        exclude group: 'mezz.jei'
    }

    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    compile "curse.maven:simpleharvest:2897811"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-${top_version}"
}

repositories {
    maven { 
        name = 'Patchouli'
        url = "https://maven.blamejared.com" 
    }
    maven {
        url = "https://github.com/TehNut/temporary-maven-thing/tree/master/maven"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
    maven {
        name = "JER"
        url = "https://dl.bintray.com/way2muchnoise/dev"
    }
    maven {
        url 'https://maven.mcmoddev.com'
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        url "http://maven.tterrag.com"
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'roots_at.cfg'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}